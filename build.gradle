plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"

	id 'maven-publish'
	id 'checkstyle'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven { url 'https://jitpack.io' }
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"enchantment-text-styling" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

sourceSets {
	testmod {
		resources {
			srcDirs += [
					'src/testmod/generated'
			]
		}
	}

	testmodClient {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
		compileClasspath += client.compileClasspath
		runtimeClasspath += client.runtimeClasspath

		compileClasspath += testmod.compileClasspath
		runtimeClasspath += testmod.runtimeClasspath
	}
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	testmodImplementation sourceSets.main.output
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// checkstyle

checkstyle {
	configFile = rootProject.file("checkstyle.xml")
	toolVersion = "10.12.1"
}

tasks.register('checkstyleAll', Checkstyle) {
	configFile = rootProject.file("checkstyle.xml")

	// fail early
	ignoreFailures false
	showViolations true

	source 'src'

	classpath = files()
}

// publishing

def minecraft_versions = [
		"1.21.2"
]

static def forVersion(String first, String last) {
	if (first == last) {
		return first
	} else {
		return "${first}-${last}"
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "8lNgRGJD"

	versionNumber = "${project.mod_version}"
	versionName = "${project.mod_version} for ${forVersion(minecraft_versions.first(), minecraft_versions.last())}"
	versionType = "alpha"

	uploadFile = remapJar

	dependencies {
		required.project "fabric-api"
	}

	syncBodyFrom = rootProject.file("README.md").text
	changelog = rootProject.file("CHANGELOG.md").text

	gameVersions = minecraft_versions

	// debugMode = true
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

afterEvaluate {
	publishing {
		publications {
			maven(MavenPublication) {
				from components.java
				groupId = maven_group
				artifactId = archives_base_name
				version = '1.20.x-0.1.0'
			}
		}
	}
}